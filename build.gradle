plugins {
    id 'com.github.johnrengelman.shadow' version "$shadow_version"
    id 'architectury-plugin' version "$architectury_plugin_version"
    id 'dev.architectury.loom' version "$loom_version" apply false
    id 'java'
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'

    version "${mod_version}+${minecraft_version}"
    group maven_group

    archivesBaseName = rootProject.archivesBaseName + '-' + name
    loom.silentMojangMappingsLicense()

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }

        withSourcesJar()
    }

    repositories {
        maven {
            name 'Lazurite'
            url 'https://lazurite.dev/releases'
        }
        mavenLocal()
    }

    dependencies {
        minecraft "com.mojang:minecraft:$minecraft_version"
        mappings loom.officialMojangMappings()
    }

    publishing {
        repositories {
            maven {
                name 'Lazurite'
                url 'https://lazurite.dev/releases'
                credentials(PasswordCredentials)
                authentication {
                    register("basic", BasicAuthentication)
                }
            }
        }

        publications {
            "${project.name}"(MavenPublication) {
                artifactId = archivesBaseName
                from components.java
            }
        }
    }
}